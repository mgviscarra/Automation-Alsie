plugins {
    id 'org.springframework.boot' version '1.5.2.RELEASE'
    id 'com.craigburke.client-dependencies' version '1.4.0'
}

group 'spring-boot-mvc-test'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'war'

// analysis stuff
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

// Note: to run spring boot app, just type `./gradlew bootRun`, and browse http://localhost:8080/
// to build and test, run ./gradlew build
// clean, run ./gradlew clean

repositories {
    mavenCentral()
}

// demonstrating using the client-dependencies plugin for using npm to get some JS libraries
// you need to run ./gradlew clientRefresh to pull these in.
// https://github.com/craigburke/client-dependencies-gradle
clientDependencies {
    installDir = 'src/main/webapp/lib'
    npm {
        'angular'('1.5.8')
        'bootstrap'('3.3.6')
        'restangular'('1.5.1')
        'angular-animate'('1.5.0', into: 'angular/modules')
        'animate.css'('1.0.0', url:'daneden/animate.css')
        'angular-ui-bootstrap'('1.3.x', from:'dist') {
            include 'ui-bootstrap-tpls.js'
        }
        'ag-grid'('8.*')
    }
}

war {
    baseName = 'MyStudentTestApp'
    version =  '1.0.1'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

checkstyle {
    toolVersion "7.5.1"
    ignoreFailures false
    maxErrors = 100
    maxWarnings = 100
}

processResources.dependsOn clientInstall
clean.dependsOn clientClean

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.4'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '4.1.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    compile group: 'io.cucumber', name: 'cucumber-java', version: '4.2.0'
    testCompile group: 'io.cucumber', name: 'cucumber-junit', version: '4.2.0'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version: '3.141.59'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-api', version: '3.141.59'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-remote-driver', version: '3.141.59'

}

test {
    useJUnit()
    systemProperties = [
            'webdriver.chrome.driver': '/Users/mauricio.viscarra/Documents/driver/chromedriver',
            'browser': 'chrome'
    ]

}